plugins {
    id "com.github.johnrengelman.shadow" version "7.1.1"
    id "io.freefair.lombok" version "8.6"
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: rootDir.toString() + '/dependencies.gradle'

group = properties['group']
version = properties['version']

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

ext {
    pluginMain = properties['plugin.main'] == 'default' ?
            "${project.group}.${project.name.toLowerCase()}.${project.name}" :
            properties['plugin.main']
}

repositories {
    mavenCentral()
    jcenter()
    // JitPack
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.glaremasters.me/repository/concuncan/' }
    maven { url 'https://repo.papermc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://jcenter.bintray.com/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.opencollab.dev/maven-releases/' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

disableAutoTargetJvm()

dependencies {
    // Defaults.
//    compileOnly(files(FILES))
    annotationProcessor(ANNO)
    implementation(IMPL)
    compileOnly(COMP_ONLY)
    shadow(SHADOW)

    // Other Plugins
//    compileOnly(OTHER_PLUGINS)
}

processResources {
    // Debugging: Print values
    doFirst {
        println "Version: ${project.version}, Name: ${project.name}, Main: ${project.ext.pluginMain}"
    }

    inputs.property('name', "${project.name}")
    inputs.property('version', "${project.version}")
    inputs.property('main', "${project.ext.pluginMain}")

    filesMatching('**/plugin.yml') {
        expand (
                'name': "${project.name}",
                'version': "${project.version}",
                'main': "${project.ext.pluginMain}",
        )
    }
}

shadowJar {
//    setArchiveFileName(project.name + "-" + project.version + ".jar")

    relocate('com.github.Anon8281.universalScheduler', 'net.streamline.libs.universalScheduler')
}

artifacts {
    archives shadowJar
}

wrapper {
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.ALL
}