plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2' // for shadowing
    id "io.freefair.lombok" version "8.6"
}

apply from: rootDir.toString() + '/dependencies.gradle'

group = properties['group']
version = properties['version']

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

ext {
    pluginMain = properties['plugin.main'] == 'default' ?
            "${project.group}.${project.name.toLowerCase()}.${project.name}" :
            properties['plugin.main']
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven { url 'https://repo.papermc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

disableAutoTargetJvm()

dependencies {
    // Defaults.
    implementation(compileOnly(files(FILES)))
    annotationProcessor(ANNO)
    implementation(IMPL)
    compileOnly(COMP_ONLY)
    shadow(SHADOW)

    // TheBase
    implementation('com.github.simplix-softworks:simplixstorage:3.2.7')
    shadow('com.github.simplix-softworks:simplixstorage:3.2.7')

    implementation('mysql:mysql-connector-java:8.0.33')
    shadow('mysql:mysql-connector-java:8.0.33')

    implementation('org.xerial:sqlite-jdbc:3.46.1.0')
            .exclude(group: 'org.slf4j', module: 'slf4j-api')
    shadow('org.xerial:sqlite-jdbc:3.46.1.0')
            .exclude(group: 'org.slf4j', module: 'slf4j-api')

    implementation('com.zaxxer:HikariCP:5.1.0')
            .exclude(group: 'org.slf4j', module: 'slf4j-api')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-api')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-core')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl')
    shadow('com.zaxxer:HikariCP:5.1.0')
            .exclude(group: 'org.slf4j', module: 'slf4j-api')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-api')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-core')
            .exclude(group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl')

    implementation('net.objecthunter:exp4j:0.4.8')
    shadow('net.objecthunter:exp4j:0.4.8')

    implementation('com.google.re2j:re2j:1.7')
    shadow('com.google.re2j:re2j:1.7')

    // OblivateInvs
    implementation("com.github.hamza-cskn.obliviate-invs:core:4.1.12").exclude(group: 'com.google.code.gson', module: 'gson')
    shadow("com.github.hamza-cskn.obliviate-invs:core:4.1.12").exclude(group: 'com.google.code.gson', module: 'gson')

    // Other Plugins
    compileOnly(OTHER_PLUGINS)
}

processResources {
    // Debugging: Print values
    doFirst {
        println "Version: ${project.version}, Name: ${project.name}, Main: ${project.ext.pluginMain}"
    }

    inputs.property('name', "${project.name}")
    inputs.property('version', "${project.version}")
    inputs.property('main', "${project.ext.pluginMain}")

    filesMatching('**/plugin.yml') {
        expand (
                'name': "${project.name}",
                'version': "${project.version}",
                'main': "${project.ext.pluginMain}",
        )
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11)) // Change this to your desired version
    }
}

shadowJar {
    archiveClassifier.set('')

    archiveFileName = "${project.name}-${project.version}.jar"

    relocate('com.github.Anon8281.universalScheduler', 'host.plas.bou.libs.universalScheduler')
    relocate('com.github.benmanes.caffeine', 'host.plas.bou.libs.caffeine')
    relocate('com.google.gson', 'host.plas.bou.libs.gson')

    relocate('de.leonhard', 'tv.quaint.thebase.lib.leonhard')
}

artifacts {
    archives shadowJar
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.ALL
}